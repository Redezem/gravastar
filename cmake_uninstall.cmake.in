if(NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
    message(FATAL_ERROR "Cannot find install_manifest.txt")
endif()

file(READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" _manifest_contents)
string(REPLACE "\n" ";" _manifest_files "${_manifest_contents}")
foreach(_file ${_manifest_files})
    if(EXISTS "${_file}" OR IS_SYMLINK "${_file}")
        file(REMOVE "${_file}")
    endif()
endforeach()

set(_extra_files "")
if("@CMAKE_SYSTEM_NAME@" STREQUAL "Linux")
    if(EXISTS "/run/systemd/system")
        list(APPEND _extra_files "/etc/systemd/system/gravastar.service")
    elseif(EXISTS "/sbin/openrc-run" OR EXISTS "/sbin/rc-service" OR EXISTS "/etc/init.d")
        list(APPEND _extra_files "/etc/init.d/gravastar")
    endif()
elseif("@CMAKE_SYSTEM_NAME@" STREQUAL "OpenBSD")
    list(APPEND _extra_files "/etc/rc.d/gravastar")
endif()

foreach(_file ${_extra_files})
    if(EXISTS "${_file}" OR IS_SYMLINK "${_file}")
        file(REMOVE "${_file}")
    endif()
endforeach()

if(EXISTS "/etc/gravastar")
    file(GLOB _gravastar_configs "/etc/gravastar/*")
    if(NOT _gravastar_configs)
        file(REMOVE_RECURSE "/etc/gravastar")
    endif()
endif()

if(EXISTS "/var/gravastar")
    file(REMOVE_RECURSE "/var/gravastar")
endif()
